#!/usr/bin/perl
use File::Spec;
use File::Basename;
use FindBin::libs;
use Getopt::Long qw/:config posix_default no_ignore_case bundling auto_help/;
use Plack::Builder;
use Plack::Loader;
use Ukigumo::Server;
use Ukigumo::Server::API;
use Ukigumo::Server::Web;
use Plack::Session::Store::File;
use File::Path qw(mkpath);

GetOptions(\my %opt, qw/
    port=s
    host=s
    max-workers=i
/);
$opt{host}          //= 0;
$opt{port}          //= 2828;
$opt{'max-workers'} //= 4;

$ENV{PLACK_ENV} //= 'development';
my $session_dir = File::Spec->catdir(File::Spec->tmpdir(), "ukigumo-session-$ENV{PLACK_ENV}");
mkpath($session_dir);

{
    my $app = Ukigumo::Server->new();
    $app->setup_schema();
}

local $0 = "$0 (Ukigumo::Server)"; # steal from growthforecast.pl
my $app = builder {
    enable 'Plack::Middleware::ReverseProxy';

    my $api = Ukigumo::Server::API->to_app();
    my $ui = builder {
        enable 'Plack::Middleware::Static',
          path => qr{^(?:/static/|/robot\.txt$|/favicon.ico$)},
          root => File::Spec->catdir( Ukigumo::Server->share_dir );
        enable 'Plack::Middleware::Session',
            store => Plack::Session::Store::File->new(
                dir => $session_dir,
            );
        Ukigumo::Server::Web->to_app();
    };

    sub {
        my $env = shift;
        if ($env->{PATH_INFO} =~ m{^/api/}) {
            $api->($env);
        } else {
            $ui->($env);
        }
    };
};

my $loader = Plack::Loader->load('Starlet',
    port        => $opt{port},
    host        => $opt{host},
    max_workers => $opt{'max-workers'},
);

printf "ukigumo-server starts listen on %s:%s\n", $opt{host}, $opt{port};

$loader->run($app);

__END__

=head1 NAME

ukigumo-server - ukigumo server launcher

=head1 SYNOPSIS

    % ukigumo-server
        --host=127.0.0.1 # Bind host   (Default: 0)
        --port=80        # Bind port   (Default: 2828)
        --max-workers    # Max workers (Default: 4)

=head1 DESCRIPTION

ukigumo-server is a Ukigumo::Server launcher.
